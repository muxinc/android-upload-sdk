import org.jetbrains.dokka.DokkaConfiguration.Visibility
import org.jetbrains.dokka.gradle.DokkaTask

plugins {
  id 'com.android.library'
  id 'org.jetbrains.kotlin.android'
  id 'com.mux.gradle.android.mux-android-distribution'
  id 'org.jetbrains.dokka'
}

android {
  namespace 'com.mux.video.upload'
  compileSdk 33

  defaultConfig {
    minSdk 16
    targetSdk 33

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    consumerProguardFiles "consumer-rules.pro"
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  kotlinOptions {
    jvmTarget = '1.8'
  }
}

muxDistribution {
  devVersion versionFromCommitHash('dev-')
  releaseVersion versionFromTag()
  artifactIds just("upload")
  groupIds just("com.mux.video")
  publicReleaseIf releaseOnTag()

  // TODO: Better make the distro plugin use the same config as the dokka. How can you change what withJavaDocJar does
  packageJavadocs = false
  packageSources = true
  publishIf { it.containsIgnoreCase("release") }
  artifactoryConfig {
    contextUrl = "https://muxinc.jfrog.io/artifactory/"
    releaseRepoKey = 'default-maven-release-local'
    devRepoKey = 'default-maven-local'
  }
}

tasks.withType(DokkaTask.class) {
  dokkaSourceSets.configureEach {
    documentedVisibilities.set([Visibility.PUBLIC, Visibility.PROTECTED])

    moduleName.set("Mux Upload SDK")

    if (project.file("Module.md").exists()) {
      includes.from(project.file("Module.md"))
    }

    perPackageOption {
      matchingRegex.set(".*internal.*")
      suppress.set(true)
    }
  }
}

dependencies {
  implementation 'androidx.core:core-ktx:1.9.0'

  implementation "com.squareup.okhttp3:logging-interceptor:4.11.0"
  implementation "com.squareup.okhttp3:okhttp:4.11.0"

  implementation 'androidx.core:core-ktx:1.9.0'
  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.0"

  testImplementation 'junit:junit:4.13.2'
  testImplementation 'androidx.test.ext:junit:1.1.5'
  testImplementation "io.mockk:mockk:1.13.5"
  testImplementation 'org.robolectric:robolectric:4.10.2'
}
