plugins {
  id 'com.android.library'
  id 'org.jetbrains.kotlin.android'
  id 'com.mux.gradle.android.mux-android-distribution'
}

android {
  namespace 'com.mux.video.upload'
  compileSdk 34

  defaultConfig {
    minSdk 16
    targetSdkVersion 34

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    consumerProguardFiles "consumer-rules.pro"
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  kotlinOptions {
    jvmTarget = '1.8'
  }
}

muxDistribution {
  devVersion versionFromCommitHash('dev-')
  releaseVersion versionFromTag()
  artifactIds just("upload")
  groupIds just("com.mux.video")
  publicReleaseIf releaseOnTag()

  packageJavadocs = true
  packageSources = true
  publishIf { it.containsIgnoreCase("release") }
  artifactoryConfig {
    contextUrl = "https://muxinc.jfrog.io/artifactory/"
    releaseRepoKey = 'default-maven-release-local'
    devRepoKey = 'default-maven-local'
  }
  dokkaConfig {
    moduleName = "Mux Upload SDK"
    footer = "(c) " + new Date().format("yyyy") + " Mux, Inc. Have questions or need help?" +
            " Contact support@mux.com"
  }
  pom {
    description "The Mux Upload SDK for Android"
    inceptionYear = "2023"
    url = "https://github.com/muxinc/android-upload-sdk"
    organization {
      name = "Mux, Inc"
      url = "https://www.mux.com"
    }
    developers {
      developer {
        email = "support@mux.com"
        name = "The player and sdks team @mux"
        organization = "Mux, inc"
      }
    }
  }
}

dependencies {
  implementation "com.squareup.okhttp3:logging-interceptor:4.12.0"
  implementation "com.squareup.okhttp3:okhttp:4.12.0"
  implementation "io.github.crow-misia.libyuv:libyuv-android:0.31.1"

  implementation 'androidx.core:core-ktx:1.12.0'
  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3"

  testImplementation 'junit:junit:4.13.2'
  testImplementation 'androidx.test.ext:junit:1.1.5'
  testImplementation "io.mockk:mockk:1.13.9"
  testImplementation 'org.robolectric:robolectric:4.11.1'
}
